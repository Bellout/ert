import os
import os.path
import sys
sys.path += ["../../../build"]
import global_config
from   global_config import LIBUTIL
from   global_config import LIBECL
from   global_config import LIBPLOT
from   global_config import LIBCONFIG
from   global_config import LIBJOB_QUEUE
from   global_config import LIBENKF
from   global_config import LIBRMS
from   global_config import LIBSCHED

from   global_config import add_program

package      = "enkf"
lib_path     = "../lib"
include_path = "../include"
bin_path     = "../bin"  

conf = global_config.get_conf(os.getcwd() , package , 3)
env  = Environment()

conf.update_env( env , [ LIBENKF , LIBRMS , LIBJOB_QUEUE , LIBSCHED , LIBECL , LIBPLOT , LIBCONFIG , LIBUTIL ] , ["bat"  ,"lsf" , "nsl" , "lapack" , "g2c" , "plplotd" , "m" , "z" , "pthread"] , link = True)


define_fmt = "\'%s=\"%s\"\'"
tui_objects = env.Object( ["enkf_tui_main.c" , "enkf_tui_fs.c" , "enkf_tui_ranking.c" , "enkf_tui_misc.c" , "enkf_tui_table.c" , "enkf_tui_plot.c" , 
                           "enkf_tui_run.c", "enkf_tui_util.c" , "enkf_tui_init.c" , "enkf_tui_export.c"])
main_object = env.Object( "main.c" , CPPDEFINES = [define_fmt % ("SITE_CONFIG_FILE"   ,  conf.SITE_CONFIG_FILE),
                                                   define_fmt % ("SVN_VERSION"        ,  conf.SVN_VERSION),
                                                   define_fmt % ("COMPILE_TIME_STAMP" ,  conf.TIME_STAMP)])

ert         = env.Program( "ert"                       , [main_object, tui_objects])
ert_svn     = env.Program( "ert_%s" % conf.SVN_VERSION , [main_object, tui_objects])

env.InstallProgram( bin_path , ert )
SDP_INSTALL = env.Alias('SDP_INSTALL' , env.InstallProgram( conf.SDP_ERT_RELEASE , ert_svn ))

Default( bin_path )
